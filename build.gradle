

allprojects {
    group = 'mil.army.usace.hec.cwms'
    version = '2.5-SNAPSHOT'   // ApiServlet.VERSION should be updated to match MAJOR.MINOR changes.
}

// versions for shared dependencies
ext.dropwizard_version="4.2.12"

ext.JAVALIN_VERSION='4.6.4'

ext.JACKSON_VERSION='2.13.3'
ext.SWAGGER_CORE_VERSION='2.2.2'
ext.tomcatVersion='9.0.64'

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    println 'Checkstyle version:' + checkstyle.toolVersion


    compileJava{
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    repositories {
        mavenLocal()
        maven {
            url = uri('https://www.hec.usace.army.mil/nexus/repository/maven-public')
        }

        mavenCentral().content {
            // The jai_core on maven central has the pom but not the jar.
            excludeModule("javax.media", "jai_core")
        }

        maven {
            url = uri('https://repo.maven.apache.org/maven2')
        }

    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    test {
        useJUnitPlatform() {
            excludeTags "integration"
        }
    }

    checkstyleMain {
        enabled = gradle.startParameter.taskNames.contains('checkstyleMain')
    }
}
